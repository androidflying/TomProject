if (isBuildModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'org.greenrobot.greendao'


android {
    compileSdkVersion rootProject.ext.android["COMPILE_SDK_VERSION"]

    defaultConfig {
        minSdkVersion rootProject.ext.android["MIN_SDK_VERSION"]
        targetSdkVersion rootProject.ext.android["TARGET_SDK_VERSION"]
        versionCode 1
        versionName "1.0.0"

        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName(), AROUTER_GENERATE_DOC: "enable"]
            }
        }

    }

    buildTypes {
        debug {
            buildConfigField "boolean", "IS_BUILD_MODULE", "${isBuildModule}"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            buildConfigField "boolean", "IS_BUILD_MODULE", "${isBuildModule}"
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (isBuildModule.toBoolean()) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/release/AndroidManifest.xml'
            }
            jniLibs.srcDirs = ['libs']
        }
    }

}

greendao {
    //数据库的schema版本，也可以理解为数据库版本号
    schemaVersion rootProject.ext.greendaoVersion
    //设置DaoMaster、DaoSession、Dao包名，也就是要放置这些类的包的全路径。
    daoPackage 'com.tom.im.dao'
    //设置DaoMaster、DaoSession、Dao目录
    targetGenDir 'src/main/java'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation rootProject.ext.dependencies["appcompat-v7"]
    implementation rootProject.ext.dependencies["design"]
    implementation rootProject.ext.dependencies["constraint-layout"]
    implementation rootProject.ext.dependencies["eventbus"]
    implementation rootProject.ext.dependencies["glide"]
    implementation rootProject.ext.dependencies["greendao"]
    implementation rootProject.ext.dependencies["gson"]
    //公共业务类库（必备）
    implementation project(':CommonSDK')
    //加强版WebView（常用）
    implementation project(':ThirdLibrarys:AgentWeb')
    //样式依赖库 （可选）
    //implementation project(':ThirdLibrarys:QMUI')
    //轮播图控件（可选）
    //implementation project(':ThirdLibrarys:Banner')

    implementation project(':Album:EasyPhoto')

    if (isBuildModule.toBoolean()) {
        annotationProcessor rootProject.ext.dependencies["arouter-compiler"]
    } else {
        compileOnly rootProject.ext.dependencies["arouter-compiler"]
    }
    implementation project(':RongCloud:CallKit')
    implementation project(':RongCloud:CallLib')
    implementation project(':RongCloud:IMKit')
    implementation project(':RongCloud:IMLib')
}
